#!/usr/bin/env perl6

use UML::Translators;

# subset lib_type of Any where Str|False;

#| Make a UML diagram for a specified package.
sub MAIN(Str $packageName,                     #= Package name.
         Str :$type = "class",                 #= Type of the UML diagram.
         Str :I($path) = '',                   #= Using include path to find libraries.
         Bool :$attributes = True,             #= Should the class attributes be included in the UML diagrams or not?
         Bool :$methods = True,                #= Should the class methods be included in the UML diagrams or not?
         Bool :$conciseGrammarClasses = True,  #= Should grammar classes be shortened or not?
         Str :$format = 'PlantUML'             #= Format of the output, one of 'Plant', 'PlantUML', 'WL', 'WLUML', or 'Whatever'.
         ) {

    # If there is a library argument
    if $path.chars > 0 {
        given $path {
            when $_.IO.d { CompUnit::RepositoryRegistry.use-repository(CompUnit::Repository::FileSystem.new(prefix => $path)) }
            when $_.IO.f { note "$path if file not a directory." }
            when !$_.IO.e { note "Unknown directory '$path'." }
        }
    }

    # Call the main function
    my Str $res;
    if $format.lc ∈ <whatever automatic plantuml plant-uml plant> {
        $res = to-plant-uml-spec($packageName, :$type, :$attributes, :$methods, :$conciseGrammarClasses);
    } elsif $format.lc ∈ <wl wluml wl-uml wlumlgraph wl-uml-graph mathematica> {
        $res = to-wl-uml-spec($packageName, :$type, :$attributes, :$methods, :$conciseGrammarClasses);
    } else {
        die "Uknown format $format. The format value is expected to be one of 'Plant', 'PlantUML', 'WL', 'WLUML', or 'Whatever'";
    }

    # Result
    say $res;
}